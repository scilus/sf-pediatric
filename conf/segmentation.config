/*
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Config file for defining DSL2 per module options and publishing paths
~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~
    Available keys to override module options:
        ext.args   = Additional arguments appended to command in module.
        ext.args2  = Second set of arguments appended to command in module (multi-tool modules).
        ext.args3  = Third set of arguments appended to command in module (multi-tool modules).
        ext.prefix = File name prefix for output files.
----------------------------------------------------------------------------------------
*/

process {

    //** SEGMENTATION **//
    withName: '.*:SEGMENTATION:FASTSURFER' {
        ext.acq3T = params.acq3T
        ext.cerebnet = params.cerebnet
        ext.hypvinn = params.hypvinn
        ext.seg_only = false
        publishDir = [
            [
                path: { params.fs_output_dir ?: "${params.outdir}/../fastsurfer-v2.3.3/" },
                mode: params.publish_dir_mode,
                saveAs: {
                    filename ->
                    def ses = meta.session ? "_${meta.session}" : ""
                    if ( filename.contains("fastsurfer") ) { "${meta.id}${ses}" }
                    else if ( filename.contains("versions.yml") ) { null }
                    else { params.lean_output ? null : filename }
                }
            ],
            [
                path: { meta.session ? "${params.outdir}/${meta.id}/${meta.session}/anat/" : "${params.outdir}/${meta.id}/anat/" },
                mode: params.publish_dir_mode,
                saveAs: {
                    filename ->
                    def ses = meta.session ? "${meta.session}_" : ""
                    if ( filename.contains("__final_t1.nii.gz") ) { "${meta.id}_${ses}desc-preproc_T1w.nii.gz" }
                    else if ( filename.contains("versions.yml") ) { null }
                    else { params.lean_output ? null : filename }
                }
            ]
        ]
    }

    withName: '.*:SEGMENTATION:RECONALLCLINICAL' {
        publishDir = [
            path: { params.fs_output_dir ?: "${params.outdir}/../freesurfer/" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                def ses = meta.session ? "_${meta.session}" : ""
                if ( filename.contains("freesurfer") ) { "${meta.id}${ses}" }
                else if ( filename.contains("versions.yml") ) { null }
                else { params.lean_output ? null : filename }
            }
        ]
    }

    withName: '.*:SEGMENTATION:RECONALL' {
        publishDir = [
        [
            path: { params.fs_output_dir ?: "${params.outdir}/../freesurfer/" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                def ses = meta.session ? "_${meta.session}" : ""
                if ( filename.contains("recon_all") ) { "${meta.id}${ses}" }
                else if ( filename.contains("versions.yml") ) { null }
                else { params.lean_output ? null : filename }
            }
        ],
        [
            path: { meta.session ? "${params.outdir}/${meta.id}/${meta.session}/anat/" : "${params.outdir}/${meta.id}/anat/" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                def ses = meta.session ? "${meta.session}_" : ""
                if ( filename.contains("__final_t1.nii.gz") ) { "${meta.id}_${ses}desc-preproc_T1w.nii.gz" }
                else if ( filename.contains("versions.yml") ) { null }
                else { params.lean_output ? null : filename }
            }
        ]
        ]
    }
    withName: '.*:SEGMENTATION:MCRIBS' {
        ext.surf = true
        ext.jointhresh = params.mcribs_jointhresh
        ext.fastcollision = params.mcribs_fastcollision
        ext.nopialoutside = params.mcribs_nopialoutside
        ext.cortical = true
        ext.aparcaseg = true
        ext.stats = true
        ext.seed = params.mcribs_seed
        publishDir = [
            path: { params.fs_output_dir ?: "${params.outdir}/../mcribs-2.1.0/" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                def ses = meta.session ? "_${meta.session}" : ""
                if ( filename.contains("mcribs") ) { "${meta.id}${ses}" }
                else if ( filename.contains("versions.yml") ) { null }
                else { params.lean_output ? null : filename }
            }
        ]
    }

    withName: '.*:SEGMENTATION:BRAINNETOMECHILD' {
        publishDir = [
            [
                path: { meta.session ? "${params.outdir}/${meta.id}/${meta.session}/anat/" : "${params.outdir}/${meta.id}/anat/" },
                mode: params.publish_dir_mode,
                saveAs: {
                    filename ->
                    def ses = meta.session ? "${meta.session}_" : ""
                    if ( filename.contains("child_v1.nii.gz") ) { "${meta.id}_${ses}seg-BrainnetomeChild_dseg.nii.gz" }
                    else if ( filename.contains("child_v1_dilated.nii.gz") ) {"${meta.id}_${ses}seg-BrainnetomeChild_desc-dilated_dseg.nii.gz"}
                    else if ( filename.contains("subcortical.tsv") ) {"${meta.id}_${ses}seg-BrainnetomeChild_stat-subcortical.tsv"}
                    else if ( filename.contains("volume_lh") ) {"${meta.id}_${ses}seg-BrainnetomeChild_hemi-left_stat-volume.tsv"}
                    else if ( filename.contains("volume_rh") ) {"${meta.id}_${ses}seg-BrainnetomeChild_hemi-right_stat-volume.tsv"}
                    else if ( filename.contains("thickness_lh") ) {"${meta.id}_${ses}seg-BrainnetomeChild_hemi-left_stat-thickness.tsv"}
                    else if ( filename.contains("thickness_rh") ) {"${meta.id}_${ses}seg-BrainnetomeChild_hemi-right_stat-thickness.tsv"}
                    else if ( filename.contains("area_lh") ) {"${meta.id}_${ses}seg-BrainnetomeChild_hemi-left_stat-surfacearea.tsv"}
                    else if ( filename.contains("area_rh") ) {"${meta.id}_${ses}seg-BrainnetomeChild_hemi-right_stat-surfacearea.tsv"}
                    else if ( filename.contains("brainnetome_child_v1_LUT.txt") ) {"${meta.id}_${ses}seg-BrainnetomeChild_desc-labels_dseg.txt"}
                    else if ( filename.contains("brainnetome_child_v1_LUT.json") ) {"${meta.id}_${ses}seg-BrainnetomeChild_desc-labels_dseg.json"}
                    else if ( filename.contains("versions.yml") ) { null }
                    else { params.lean_output ? null : filename }
                }
            ],
            [
                path: {
                    def ses = meta.session ? "_${meta.session}" : ""
                    params.fs_output_dir ? "${params.fs_output_dir}/${meta.id}${ses}/${meta.id}${ses}/label/" : params.method == "fastsurfer" ? "${params.outdir}/../fastsurfer-v2.3.3/${meta.id}${ses}/${meta.id}${ses}/label/" : "${params.outdir}/../freesurfer/${meta.id}${ses}/${meta.id}${ses}/label/" },
                mode: params.publish_dir_mode,
                pattern: "*BN_Child.annot",
            ],
            [
                path: {
                    def ses = meta.session ? "_${meta.session}" : ""
                    params.fs_output_dir ? "${params.fs_output_dir}/${meta.id}${ses}/${meta.id}${ses}/stats/" : params.method == "fastsurfer" ? "${params.outdir}/../fastsurfer-v2.3.3/${meta.id}${ses}/${meta.id}${ses}/stats/" : "${params.outdir}/../freesurfer/${meta.id}${ses}/${meta.id}${ses}/stats/" },
                mode: params.publish_dir_mode,
                pattern: "*BN_Child.stats"
            ]
        ]
    }

    withName: '.*:SEGMENTATION:FORMATLABELS' {
        publishDir = [
            path: { meta.session ? "${params.outdir}/${meta.id}/${meta.session}/anat/" : "${params.outdir}/${meta.id}/anat/" },
            mode: params.publish_dir_mode,
            saveAs: {
                filename ->
                def ses = meta.session ? "${meta.session}_" : ""
                if ( filename.contains("labels.nii.gz") ) { "${meta.id}_${ses}seg-DK_dseg.nii.gz" }
                else if ( filename.contains("volume_aseg_subcortical.tsv") ) { "${meta.id}_${ses}seg-DK_stat-subcortical.tsv" }
                else if ( filename.contains("volume_lh") ) { "${meta.id}_${ses}seg-DK_hemi-left_stat-volume.tsv" }
                else if ( filename.contains("volume_rh") ) { "${meta.id}_${ses}seg-DK_hemi-right_stat-volume.tsv" }
                else if ( filename.contains("area_lh") ) { "${meta.id}_${ses}seg-DK_hemi-left_stat-surfacearea.tsv" }
                else if ( filename.contains("area_rh") ) { "${meta.id}_${ses}seg-DK_hemi-right_stat-surfacearea.tsv" }
                else if ( filename.contains("thickness_lh") ) { "${meta.id}_${ses}seg-DK_hemi-left_stat-thickness.tsv" }
                else if ( filename.contains("thickness_rh") ) { "${meta.id}_${ses}seg-DK_hemi-right_stat-thickness.tsv" }
                else if ( filename.contains("DK_v1_LUT.json") ) { "${meta.id}_${ses}seg-DK_desc-labels_dseg.json" }
                else if ( filename.contains("DK_v1_LUT.txt") ) { "${meta.id}_${ses}seg-DK_desc-labels_dseg.txt" }
                else if ( filename.contains("versions.yml") ) { null }
                else { params.lean_output ? null : filename }
            }
        ]
    }

    withName: '.*:SEGMENTATION:CONCATENATESTATS' {
        publishDir = [
            path: { "${params.outdir}/"},
            mode: params.publish_dir_mode,
            saveAs: {
                filename -> filename.equals('versions.yml') ? null : filename
            }
        ]
    }
}
